{
  pname,
  version,
  deps ? [],
}: {
  stdenv,
  fpm,
  fakeroot,
  rpm,
  lib,
}: let
  deps_path = lib.flatten (map (d:
    if __typeOf d == "path"
    then import d
    else [])
  deps);
  deps_str = __filter (d: __typeOf d == "string") deps;
  deps' = deps_path ++ deps_str;
in
  stdenv.mkDerivation rec {
    inherit pname version;
    src = builtins.path {
      path = ./. + "/${pname}";
      name = "${pname}-source";
    };
    nativeBuildInputs = [
      fpm
      fakeroot
      rpm
    ];
    buildPhase = ''
      set -x
      trap "set +x" ERR
      fakeroot fpm ${lib.concatMapStringsSep " " (d: "--depends ${d}") deps'} \
        --input-type dir \
        --output-type rpm \
        --version ${version} \
        --name ${pname} \
        --chdir $src \
        --exclude "*.spec" \
        --architecture all \
        --log debug \
        --description "${pname} generated by Nix" \
        --rpm-compression none \
        .
      set +x
    '';
    installPhase = ''
      mkdir -p $out
      install -Dm444 *.rpm $out
    '';
  }
